<html><head><title>Digital Synthesis</title>
<link rel='stylesheet' href='./../../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../../scdoc.js' type='text/javascript'></script>
<script src='./../../docmap.js' type='text/javascript'></script>
<script src='./../../prettify.js' type='text/javascript'></script>
<script src='./../../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./../..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider SSFMT/LECTURES (extension)</div>
<div id='categories'><a href='./../../Browse.html#Tutorials/SSfMT'>Tutorials/SSfMT</a></div>
<h1>Digital Synthesis</h1>
<div id='summary'>Software Systems for Music Technology</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../../SSfMT.html">Software Systems for Music Technology</a>, <a href="./../../SSfMT/Practicals/Playing with UGens 1.html">Playing with UGens 1</a>, <a href="./../../SSfMT/Practicals/Playing with UGens 2.html">Playing with UGens 2</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#Digital%20Synthesis'>Digital Synthesis</a></li>
<ul class='toc'><li class='toc2'><a href='#Some%20Synthesis%20Techniques'>Some Synthesis Techniques</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Sample-based%20synthesis'>Sample-based synthesis</a></li>
<ul class='toc'><li class='toc2'><a href='#Digital%20Playback'>Digital Playback</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Buffers'>Buffers</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Buffers%20in%20SC'>Buffers in SC</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Types%20of%20Digital%20Playback'>Types of Digital Playback</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#PlayBuf'>PlayBuf</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Buffer%20class'>Buffer class</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#DiskIn'>DiskIn</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Chipmunk%20conclusions:'>Chipmunk conclusions:</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Fixed-Waveform%20Table-lookup%20Synthesis'>Fixed-Waveform Table-lookup Synthesis</a></li>
<ul class='toc'><li class='toc2'><a href='#Wavetable-lookup%20Synthesis:'>Wavetable-lookup Synthesis:</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Wavetable%20synthesis%20in%20SC'>Wavetable synthesis in SC</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Additive%20Synthesis'>Additive Synthesis</a></li>
<ul class='toc'><li class='toc2'><a href='#Additive%20through%20Wave%20shaping'>Additive through Wave shaping</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Additive%20through%20Summation'>Additive through Summation</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Time-varying%20Additive%20Synthesis'>Time-varying Additive Synthesis</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Multiple%20Wavetable%20Synthesis'>Multiple Wavetable Synthesis</a></li>
<ul class='toc'><li class='toc2'><a href='#Wavetable%20crossfading'>Wavetable crossfading</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Wave%20stacking'>Wave stacking</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Modulation%20Synthesis'>Modulation Synthesis</a></li>
<ul class='toc'><li class='toc2'><a href='#Ring%20Modulation%20(RM)'>Ring Modulation (RM)</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Amplitude%20Modulation%20(AM):'>Amplitude Modulation (AM):</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Frequency%20Modulation%20(FM)'>Frequency Modulation (FM)</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Phase%20modulation'>Phase modulation</a></li>
<ul class='toc'></ul></ul></ul></div><h2><a class='anchor' name='Digital%20Synthesis'>Digital Synthesis</a></h2>

<p>Digital Synthesis is the generation of waveforms with units (values) usually generated digitally and then transferred through a D/A converter to produce sound. Waveforms can be generated through mathematical formulae, A/D conversion, analysis and resynthesis of recorded sound, and other non-standard techniques.<h3><a class='anchor' name='Some%20Synthesis%20Techniques'>Some Synthesis Techniques</a></h3>
<ul>
<li>Sample-based synthesis<li>Additive synthesis<li>Subtractive synthesis<li>Wavetable synthesis<li>Amplitude Modulation (AM)<li>Frequency Modulation (FM)<li>Ring Modulation (RM)<li>Granular synthesis<li>Spectral Modelling<li>Physical Modelling<li>Non-standard synthesis</ul>
<h2><a class='anchor' name='Sample-based%20synthesis'>Sample-based synthesis</a></h2>

<p>Sample-based synthesis is based on recorded sounds and the manipulation of this information.<h3><a class='anchor' name='Digital%20Playback'>Digital Playback</a></h3>

<p>Digital playback uses stored samples from a recording of a signal that has been digitalised through an A/D converter.<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/playback.png'/><br><b>Revox Studer Reel-to-Reel Tape Machine - old style playback!</b></div>
<h3><a class='anchor' name='Buffers'>Buffers</a></h3>

<p>Digital playback requires <strong>Buffers</strong>.<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/buffers.png'/><br><b>Buffers are like containers of digital floating point values</b></div>
<ul>
<li>A buffer is a container of data loaded into the computer memory<li>Often used to hold sampled audio<li>Can be used to hold other types of data as well<li>Available as an array of floating-point numbers<li>There are different ways in which we can use buffers including playback, envelope, wavetable</ul>
<h3><a class='anchor' name='Buffers%20in%20SC'>Buffers in SC</a></h3>
<ul>
<li><code class='code prettyprint lang-sc'>Buffers</code> are arrays of 32-bit floating point values.<li><code class='code prettyprint lang-sc'>Buffers</code> are on the server so that they can be used by synths.<li><code class='code prettyprint lang-sc'>Buffers</code> need to be allocated, filled, and at some point freed.<li><code class='code prettyprint lang-sc'>Buffers</code> can be allocated, loaded and freed while synthesis is running.<li><code class='code prettyprint lang-sc'>Buffers</code> can be used for wave tables, sample buffers, delay lines, ...</ul>

<p>The number of buffers on a server is set at boot time.<pre class='code prettyprint lang-sc'>o = ServerOptions.new;
o.numBuffers;</pre>
<h3><a class='anchor' name='Types%20of%20Digital%20Playback'>Types of Digital Playback</a></h3>

<p>2 main types:<ol>
<li>Load sound file in its entirety into a buffer and then use a sample playback oscillator. This allows substantial playback manipulation.<ul>
<li>In SC this is done with the <code class='code prettyprint lang-sc'>PlayBuf</code> UGen.</ul>

<p><li>Read directly from hard drive. Not as much playback manipulation. Uses a smaller buffer, which refreshes itself.<ul>
<li>In SC this is done with the <code class='code prettyprint lang-sc'>DiskIn</code> UGen.</ul>
</ol>
<h3><a class='anchor' name='PlayBuf'>PlayBuf</a></h3>

<p><code class='code prettyprint lang-sc'>PlayBuf</code> is a UGen to listen/manipulate sound files.
<p><code class='code prettyprint lang-sc'>PlayBuf</code> - sample playback oscillator (numChannels, bufnum, rate, trigger, startPos, loop)<ul>
<li><em>rate</em>: 1.0 is the server's sample rate, 2.0 is one octave up, 0.5 is one octave down<li><em>loop</em> = 1 is true, 0 is false</ul>
<h3><a class='anchor' name='Buffer%20class'>Buffer class</a></h3>

<p>Buffers can also be accessed with the <code class='code prettyprint lang-sc'>Buffer</code> class (which may be more convenient than the messaging style)
<p><code class='code prettyprint lang-sc'>Buffer.read</code> allocates a buffer and reads a sound file into it.<pre class='code prettyprint lang-sc'>b = Buffer.read(s, "/Users/federicoreuben/Music/MastersExamples/11 Debra.aiff"); //change to your soundfile path

{ PlayBuf.ar(2, b.bufnum, 1, loop: 1) }.scope;</pre>

<p>Mouse control of rate (transposition)<pre class='code prettyprint lang-sc'>{ PlayBuf.ar(1, b.bufnum, MouseX.kr(0.5,2), loop: 1) }.scope;

b.bufnum;   // returns buffer number
b.numChannels;   // returns the number of channels in the buffer

b.free;  // return the memory and free buffer ID, IMPORTANT</pre>

<p><code class='code prettyprint lang-sc'>BufRateScale</code>
<p>File being read into a buffer may have a different sample than SC is using
<p><code class='code prettyprint lang-sc'>BufRateScale.kr(bufnum)</code> returns a ratio to adjust the playback of the sound file (for example with different sampling rates).<pre class='code prettyprint lang-sc'>b = Buffer.read(s, "/Users/federicoreuben/Music/MastersExamples/04 4 bit 9d api+e+6 [126.26].wav"); //change to your sound file path</pre>

<p>to scale the playback:<pre class='code prettyprint lang-sc'>{ PlayBuf.ar(2, b.bufnum, BufRateScale.kr(b.bufnum), loop: 1) }.scope;

b.free;</pre>

<p><code class='code prettyprint lang-sc'>BufFrames</code>
<p><code class='code prettyprint lang-sc'>BufFrames.kr(bufnum)</code> returns the current number of allocated frames
<p>it can be used to specify starting halfway through the buffer or some place else<pre class='code prettyprint lang-sc'>b = Buffer.loadDialog; //choose sound file
(
var start = 1/2;
{ PlayBuf.ar(2, b.bufnum, BufRateScale.kr(b.bufnum),
    startPos: BufFrames.kr(b.bufnum) * start, loop: 1)
}.scope;
)</pre>

<p>note that loop returns to the beginning in the above example
<p>a trigger (&lt;= 0 to &gt; 0) causes the playback to start from startPos
<p>using a trigger without looping
<p><code class='code prettyprint lang-sc'>Impulse.kr</code> produces a trigger with a certain frequency<pre class='code prettyprint lang-sc'>(
var start = 1/2;
{ PlayBuf.ar(2, b.bufnum, BufRateScale.kr(b.bufnum),
    Impulse.kr(1/3),
    startPos: BufFrames.kr(b.bufnum) * start, loop: 0)
}.scope;
)</pre>

<p>Using a trigger with looping:
<p>the trigger should occur with a frequency that is
<p>(serverSampleRate / numberOfFrames) * rateScale
<p>Server's sample rate:<pre class='code prettyprint lang-sc'>s.sampleRate;</pre>
<pre class='code prettyprint lang-sc'>(
var start = 1/2;
{ PlayBuf.ar(2, b.bufnum, BufRateScale.kr(b.bufnum),
    Impulse.kr(s.sampleRate / (BufFrames.kr(b.bufnum) * start) *
        BufRateScale.kr(b.bufnum)),
    startPos: BufFrames.kr(b.bufnum) * start, loop: 1)
}.scope;
)

b.free;</pre>

<p><strong>Exercise</strong>:<ol>
<li>Play with <code class='code prettyprint lang-sc'>PlayBuf</code> loading sound files (uncompressed) and changing the rate with different values.<li>Use another UGen to modulate the rate.<li>Add an envelope to PlayBuf with different random starting points.</ol>
<pre class='code prettyprint lang-sc'>//granular example
b = Buffer.loadDialog; //choose sound file

(
a = {var signal, env;
    signal = PlayBuf.ar(b.numChannels,b.bufnum, Rand(0.5,4), startPos: BufFrames.kr(b.bufnum)*Rand(0,1));
    env = EnvGen.kr(Env.perc(0.01,0.1), doneAction:2);
    signal * env * 0.2;
}; //a function to play small grains of sound from a buffer
)

(
{
inf.do{
a.play;
rrand(0.1,0.01).yield; //wait time specificed as random numbers betweem 0.01 to 0.1
};
}.fork; //the message .fork will start a Routine, a function where you can stecify events in time
)</pre>
<h3><a class='anchor' name='DiskIn'>DiskIn</a></h3>

<p>To continuously play longer sound file from disk. One buffer must be preloaded.
<p>using <code class='code prettyprint lang-sc'>Buffer object</code>
<p>(server, path, startFrame, numChannels)<pre class='code prettyprint lang-sc'>b = Buffer.cueSoundFile(s, "/Users/federicoreuben/Music/MastersExamples/05 180db_ [130].wav", 0, 2); //or your own path

x = {DiskIn.ar(2, b.bufnum) }.play;

b.close;    // close file

//try another file:
b.cueSoundFile("/Users/federicoreuben/Music/MastersExamples/7 Broken Train.aiff", 0, 2); //or your own path

x.free; b.close; b.free;    // close and free</pre>
<h3><a class='anchor' name='Chipmunk%20conclusions:'>Chipmunk conclusions:</a></h3>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/chipmunks.png'/><br><b>Playback synthesis can sound like chipmunks!</b></div>
<ul>
<li>Playing with different rates of playback can be fun<li>Modulating rate with other UGens<li>Cutting, reversing, randomising playback, gains<li>Might introduce amplitude distortion, aliasing and spectral envelope shifting<li>Not the same as pitch shifting<li>‘munkchkinisation’ - chipmunk sounds</ul>
<h2><a class='anchor' name='Fixed-Waveform%20Table-lookup%20Synthesis'>Fixed-Waveform Table-lookup Synthesis</a></h2>
<h3><a class='anchor' name='Wavetable-lookup%20Synthesis:'>Wavetable-lookup Synthesis:</a></h3>
<ul>
<li>Digital Sound Synthesis by sending samples through digital-to-analog converter<li>For periodic sound, computer simply reads through a table of just one cycle of waveform in a list stored in memory (wavetable) again and again</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/fixed waveform table-lookup.png'/><br><b>A Wavetable!</b></div>
<ul>
<li>Algorithm for Digital Oscillator<li>Oscillator has to resample the wavetable in order to generate different frequencies</ul>
<h3><a class='anchor' name='Wavetable%20synthesis%20in%20SC'>Wavetable synthesis in SC</a></h3>

<p><code class='code prettyprint lang-sc'>Osc</code> is a table-lookup oscillator.<pre class='code prettyprint lang-sc'>b = Buffer.alloc(s, 4096, 1); //buffer with 4096 values
b.sine1(1.0!6, true, true, true); //add 6 harmonics
b.plot;

{Osc.ar(b.bufnum, [80,80.2], 0, 0.2)}.play; //detuned left/right

b.sine1(1.0/[1,2,3,4,5,6], true, true, true); //add 6 harmonics with decaying amplitudes

b.sine1(Array.rand(32,0,1), true, true, true); //change wavetable with random selection of partials
b.plot

b.sine1(Array.rand(16,0,1.0).postln, true, true, true); //change wavetable with random amplitude in partials

{Osc.ar(b.bufnum, MouseX.kr(100,1000), 0, 0.2)!2}.play;

(
{var gate;
    gate = Decay.kr(Impulse.kr(MouseX.kr(1,10)), 0.01);
    Osc.ar(b.bufnum, [80,80.2], 0, 0.2)*Env.perc.kr(0,gate)}.play;
)

b.sine1(1.0/(1..10), true, true, true);
b.plot;

b.sine1(1.0/(1..100), true, true, true);

b.sine1(Array.fill(50,{[0,1].choose}), true, true, true); //evaluate several times
b.plot;

b.sine1([1,1,1,0,0,0,1,1,1,0,0,0,0,0,0.5,0.5,0.5], true, true, true);
b.plot;
//and now this
(
{50.do{b.sine1(Array.rand(32,0,1.0), true, true, true);
    0.25.wait;
}}.fork;
)</pre>
<h2><a class='anchor' name='Additive%20Synthesis'>Additive Synthesis</a></h2>
<ul>
<li>Synthesis technique based on summation of elementary waveforms to create more complex waveform.<li>Fixed-waveform Additive Synthesis (more economic with CPU)<li>Summation of different oscillators (more versatile but more expensive in CPU)</ul>
<h3><a class='anchor' name='Additive%20through%20Wave%20shaping'>Additive through Wave shaping</a></h3>

<p>Wave shaping is a technique that uses a <a href="https://en.wikipedia.org/wiki/Transfer_function">transfer function</a> to shape a signal.
<p>Wave shaping transfer functions are designed with <a href="https://en.wikipedia.org/wiki/Chebyshev_polynomials">Chebyshev Polynomials</a> to generate harmonics.<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/chebyshev.png'/><br><b>Plot of Chebyshev Polynomials</b></div>

<p><strong>Example</strong>:
<p>Fill a buffer with cheby specs: I want the first harmonic at 0.25 amplitude, second at 0.5, third at 0.25<pre class='code prettyprint lang-sc'>b = Buffer.alloc(s, 512, 1, {arg buf; buf.chebyMsg([0.25,0.5,0.25], false)});

b.plot;

(
{Shaper.ar(
        b,
        SinOsc.ar(440, 0.5pi, MouseX.kr(0,1)), //input cosine wave
    0.5)!2; //scale output down because otherwise it goes between -1.05 and 0.5, distorting...
}.scope;
)

b.free;</pre>

<p>You can also make your own arbitrary shapers:<pre class='code prettyprint lang-sc'>b = Buffer.alloc(s, 1024, 1);
b.cheby([1, 0.5, 1, 0.125]);

(
{var sig = Shaper.ar(b, SinOsc.ar(440, 0, MouseX.kr(0,1)));
    sig ! 2;
}.scope;
)

b.free;</pre>

<p>You can calculate the transfer function as an array and then convert it to a wavetable:<pre class='code prettyprint lang-sc'>b = Buffer.alloc(s, 1024, 1);

//size must be power of two plus 1
t = Signal.chebyFill(513,[1, 0.5, 1, 0.125]);

// t.asWavetableNoWrap will convert it to the official Wavetable format at next power of two size
b.sendCollection(t.asWavetableNoWrap);  // may also use loadCollection here

b.plot;

(
{var gate, sig;
    gate = LFPulse.kr(1);
    sig = Shaper.ar(b, SinOsc.ar(440, 0, MouseX.kr(0,1)));
    sig*Env.adsr.kr(0, gate) ! 2;
}.scope;
)

b.free;</pre>

<p>Get creative with your transfer functions:<pre class='code prettyprint lang-sc'>b = Buffer.alloc(s, 1024, 1);

// or, for an arbitrary transfer function, create the data at 1/2 buffer size + 1
t = Signal.fill(513, { |i| i.linlin(0.0, 512.0, -1.0, 1.0) }); // linear function

t = Signal.fill(513, { |i| i = rrand(-1.0, 1.0) }); //random numbers between -1 and 1

t.plot;

// t.asWavetable will convert it to the official Wavetable format at twice the size
b.sendCollection(t.asWavetableNoWrap);  // may also use loadCollection here

// shaper has no effect because of the linear transfer function
(
{var gate, sig;
    gate = Decay.kr(Impulse.kr(MouseY.kr(1,10)), 0.01);
    sig = Shaper.ar(b, SinOsc.ar(440, 0, MouseX.kr(0,1)));
    sig*Env.adsr.kr(0, gate) ! 2;
}.scope;
)

// now for a twist
(
a = Signal.fill(256, { |i|
    var t = i/255.0;
    t + (0.1 * (max(t, 0.1) - 0.1) * sin(2pi * t * 80 + sin(2pi * 25.6 * t)));
});
);

a.plot;

d = (a.copy.reverse.neg) ++(Signal[0])++ a;

d.plot;

d.size;    //must be buffer size/2 + 1, so 513 is fine

b.sendCollection(d.asWavetableNoWrap);  // may also use loadCollection here

b.plot; // wavetable format!

// test shaper
(
{var gate, sig;
    gate = Decay.kr(Dust.kr(8), 0.01);
    sig = Shaper.ar(b, SinOsc.ar(440, 0, MouseX.kr(0,0.9)));
    sig*Env.perc(0.005,0.5).kr(0, gate) ! 2;
}.scope;
).scope;

//try the distortion with you input (left is unaffectes, right distorted)
//be careful, use headphones to avoid feedback
(
{var in, dist;
    in = SoundIn.ar(0);
    dist =    Shaper.ar(b, in*MouseX.kr(0,0.9));
    (in*MouseX.kr(0.9,0)) + dist;
}.scope;
)</pre>
<div class='note'><span class='notelabel'>NOTE:</span> This examples are taken from the <code class='code prettyprint lang-sc'>Shaper</code> help file</div><h3><a class='anchor' name='Additive%20through%20Summation'>Additive through Summation</a></h3>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/summation.png'/><br><b>Additive Synthesis Patch</b></div>

<p><ul>
<li>Addition of partials<li>Overtones, Harmonics<li>Inharmonic partials<li>Fixed-wave/periodic signals are limited - they can only represent a steady-state spectrum<li>Acoustic sound has a time-varying spectrum (changes millisecond by millisecond)<li>A time-varying timbre is usually more tantalising to the ear than a constant spectrum (Grey, 1975)</ul>

<p><strong>Example in SC</strong><pre class='code prettyprint lang-sc'>(
~synth = {arg freq=40, amp=0.5, lag=30;
    SinOsc.ar(freq.lag(lag),0,amp.lag(lag))!2; //lag creates a linear ramp
 };
)

(
~arr = Array.fill(12, {rrand(200,1000)}).sort;
~synthArr = [];
~arr.do{|item|
    ~synthArr = ~synthArr.add(
        ~synth.play(args: [\freq, item, \amp, 0.5/~arr.size]) ;
    );
}
)

~arr = [~arr[0]] ++ Array.fill(~arr.size-1,{rrand(~arr[0],~arr[0]*20)});
~synthArr.do{|item, index| item.set(\freq, ~arr[index].postln)};</pre>
<ul>
<li>Additive synthesis through the summation of various oscillators: <code class='code prettyprint lang-sc'>Sine, Saw, Pulse, LTFri, Klang, DynKlang, etc.</code><li>More flexibility and control</ul>
<h3><a class='anchor' name='Time-varying%20Additive%20Synthesis'>Time-varying Additive Synthesis</a></h3>
<ul>
<li>Changing mixture of sine waves over time usually results in more interesting and complex synthetic sounds<li>Each sine oscillator uses a different envelope to control amplitude<li>Frequency can also change depending on time-varying considerations (for example for ADSR spectra might change)</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/trumpet.png'/><br><b>Time-varying spectrum plot of twelve partials of a trumpet tone</b></div>

<p><em>Control Data</em> for Additive Synthesis can include:<ul>
<li>Driving functions for synthesis algorithms:<li>Imported/mapped from another domain<li>Generated by user constraints<li>Generated by interactive systems<li>Entered by user<li>Etc.</ul>

<p><strong>Example in SC</strong>:<pre class='code prettyprint lang-sc'>(
~synth =  {arg freq=40, amp=0.5, atk=0.1,dec=0.2,sus=0.5,rel=1.0,gate=1, vol=0.05;
    var signal, env;
    signal = SinOsc.ar(freq,0,amp);
    env = Env.adsr(atk,dec,sus,rel).kr(2, gate);
    (signal*env*vol)!2;
};
)

(
~rout = {inf.do{
    ~size = rrand(10,40).postln; //change number of oscillators
    ~arr = Array.fill(~size, {rrand(21,127.0).midicps}).sort.postln;
~envArr = [Array.rand(~size,0.1,2.6), Array.rand(~size,0.3,0.6), Array.rand(~size,0.8,1.0), Array.rand(~size,0.1,10.0)];
    ~newEnvArr = [];
~envArr.do{|arr|
    ~newEnvArr = ~newEnvArr.add(arr.sort.reverse);
};
~synthArr = [];
~arr.do{|item, index|
    ~synthArr = ~synthArr.add(~synth.play(args: [\freq, item, \amp, (rrand(0.5,0.1)/(~arr.size/4)), \atk, ~newEnvArr[0][index], \dec, ~newEnvArr[1][index], \sus, ~newEnvArr[2][index], \rel, ~newEnvArr[3][index]]));
};
    rrand(1, 1.5).yield;
    ~synthArr.do{|item, index| item.set(\gate, 0)};
    rrand(3.5, 6.0).yield;
}}.fork;
)

~rout.stop;

~synthArr.do{|item, index| item.free}; //if hanging</pre>
<h2><a class='anchor' name='Multiple%20Wavetable%20Synthesis'>Multiple Wavetable Synthesis</a></h2>

<p><strong>Discussion</strong>:<ul>
<li>What characterises Fixed-wavetable Synthesis?<li>What is the main problem of Fixed-wavetable Oscillators?<li>Static timbre<li>Not very interesting or dynamic result<li>Challenge in synthesis:<li>How to generate interesting time-varying timbres?</ul>
<h3><a class='anchor' name='Wavetable%20crossfading'>Wavetable crossfading</a></h3>
<ul>
<li>Simple idea really: crossfade between 2 or more fixed waveforms during event<li>Sound “mutates” from one source to another over time</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/wavetable_crossfading.png'/><br><b>Wavetable crossfading</b></div>
<pre class='code prettyprint lang-sc'>(
a = {arg freq=440, attack=0.01, dec=0.4, sus=0.3, sus2=0.4, rel=2, vol=0.5;
    var wave1, wave2, wave3, wave4;
    var env1,env2,env3,env4, globalEnv;
    env1 = Env([0,1,0],[attack,dec]).kr;
    env2 = Env([0,0,1,0],[attack,dec,sus]).kr;
    env3 = Env([0,0,1,0],[attack+dec,sus,sus2]).kr;
    env4 = Env([0,0,1,0],[attack+dec+sus,sus2,rel]).kr;
    globalEnv = Env.new([0,1,0.5,0.5,0.4,0],[attack,dec,sus,sus2,rel]).kr(2);

    wave1 = Pulse.ar(freq)*env1;
    wave2 = Formant.ar(freq)*env2;
    wave3 = Saw.ar(freq)*env3;
    wave4 = SinOsc.ar(freq)*env4;

    ////noise instead
    //wave1 = WhiteNoise.ar*env1;
    //wave2 = GrayNoise.ar*env2;
    //wave3 = PinkNoise.ar*env3;
    //wave4 = BrownNoise.ar*env4;

    Pan2.ar([wave1, wave2, wave3, wave4].sum)*globalEnv*vol;
};
)

a.play(args: [\freq, 440]);
a.play(args: [\freq, 880]);

(
{12.do{
    a.play(args: [\freq, (Scale.major.degrees+[60,72].choose).choose.midicps]);
    0.8.yield;
}}.fork;
)

(//detuned synths
{24.do{
    a.play(args: [\freq, rrand(200,800), \attack, rrand(0.01,0.05), \dec, rrand(0.2,0.4), \sus,rrand(0.1,0.3), \sus2, rrand(0.1,0.3), \rel, rrand(0.4,2.1), \vol, rrand(0.1,0.5)]);
    0.2.yield;
}}.fork;
)</pre>
<ul>
<li>Wavetable crossfading is at core of compound synthesis, vector synthesis (Sequential Circuits, Korg, Yamaha), L/A (Linear Arithmetic synthesis, Roland)<li>Prophet VS (1985, Sequential Circuits) - crossfade between 4 different waveforms<li>Later synthesisers: users specify arbitrary number of waveforms during one event</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/wavetable_crossfading2.png'/><br><b>Complex user specified wavetable crossfading</b></div>
<h3><a class='anchor' name='Wave%20stacking'>Wave stacking</a></h3>
<ul>
<li>Wavetable staking or wave stacking is a variation of additive synthesis, but:<li>Instead of sine waves, wave stacking uses different more complex signals (usually sampled) “stacked” together to create new timbre<li>Each waveform has its own amplitude envelope causing spectral changes during event<li>Storing waveforms in table-lookup oscillators<li>Also combined with wavetable crossfade for more internal motion<li>Wave stacking is similar to additive with different complex signals</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/wavestacking.png'/><br><b>Wavestacking patch</b></div>
<pre class='code prettyprint lang-sc'>(
a =  {arg freq=440, attack=0.1, sus=0.3, rel=2, vol=0.5;
    var globalEnv, harm, signal, envArr;
    var wave1, wave2, wave3, wave4;
    globalEnv = Env.linen(attack,sus,rel).kr(2);
    [attack,sus,rel].sum*[[0.3,0.6,0.1],[0.1,0.5,0.4],[0.01,0.3,0.69],[0.8,0.1,0.1]].do{|item| envArr = envArr.add(Env.linen(item[0], item[1], item[2]))};
    harm = [1,2,3,4];
    wave1 = Pulse.ar(freq*harm[3])*envArr[0].kr;
    wave2 = Formant.ar(freq*harm[1])*envArr[1].kr;
    wave3 = Saw.ar(freq*harm[2])*envArr[2].kr;
    wave4 = SinOsc.ar(freq*harm[0])*envArr[3].kr;
    signal = ((wave1 + wave2 + wave3 + wave4)/4)*globalEnv;
    Pan2.ar(signal*vol*2);
}
)

a.play(args: [\freq, 440]);
a.play(args: [\freq, 880]);

(
{12.do{
    a.play(args: [\freq, (Scale.major.degrees+[60,72].choose).choose.midicps]);
    0.8.yield;
}}.fork;
)

(//detuned synths
{24.do{
    a.play(args: [\freq, rrand(200,800), \attack, rrand(0.01,0.05), \dec, rrand(0.2,0.4), \sus,rrand(0.1,0.3), \sus2, rrand(0.1,0.3), \rel, rrand(0.4,2.1), \vol, rrand(0.1,0.5)]);
    0.2.yield;
}}.fork;
)</pre>
<h2><a class='anchor' name='Modulation%20Synthesis'>Modulation Synthesis</a></h2>
<ul>
<li>Modulation: control of an aspect of one signal with another<li>Modulated signal is the <em>carrier</em><li>Controlling signal is the <em>modulator</em><li>Modulation is nonlinear process - new frequency components can appear, we call them <em>sidebands</em></ul>

<p><strong>Bipolar and unipolar signals</strong><ul>
<li>Polarity: positive and negative polarity (+1,-1)<li>Bipolar signals have both positive and negative excursion around 0<li>Unipolar signal a constant has been added (range above 0)<li>Difference between RM and AM is that RM modulates two bipolar signals which AM modulates a bipolar with a unipolar signal</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/bipolar_unipolar.png'/><br><b>Biploar vs unipolar signals</b></div>
<h3><a class='anchor' name='Ring%20Modulation%20(RM)'>Ring Modulation (RM)</a></h3>
<ul>
<li>Theoretically a form of AM.<li>Ring Modulation (RM) is a straight multiplication of two bipolar signals. RM is symmetric (carrier and modulator are the same)<li>RingMod = C x M (Ring Modulation = Carrier signal x Modulation signal)<li>If M is bellow 20Hz = tremolo effect<li>At higher frequencies:<ul>
<li>Carrier disappears<li>For each sinusoidal component in the carrier, the modulator contributes two sidebands: the sum and the<li>difference of C and M<li>Phases of output are also the sum and difference of phases of C and M.</ul>

<p><li>If modulation frequency is higher than carrier frequency, negative frequencies occur. Negative frequency converts into positive mirror-image (with phase change)</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/ring1.png'/><br><b>Ring Modulation</b></div>

<p>Create a frequency analyser:<pre class='code prettyprint lang-sc'>FreqScope.new(400, 200, 0);</pre>

<p>Simple ring modulation:<pre class='code prettyprint lang-sc'>{SinOsc.ar(440,0,SinOsc.ar(Line.kr(1,10,20)))*0.5}.scope; //low freq ring modulation causes tremolo</pre>

<p>This is the same (a multiplication of two bipolar signals):<pre class='code prettyprint lang-sc'>{SinOsc.ar(440,0,0.5)*SinOsc.ar(Line.kr(1,10,20))}.scope; //low freq ring modulation causes tremolo</pre>

<p>With a sound file:<pre class='code prettyprint lang-sc'>b = Buffer.loadDialog;

{PlayBuf.ar(1, b.bufnum)*SinOsc.ar(10)}.play //low freq causes tremolo

{PlayBuf.ar(1, b.bufnum)*SinOsc.ar(440)}.play //high freq causes sidebands

(
{var carrier, modulator;
    carrier = 1000;
    modulator = 400;
    SinOsc.ar(carrier,0,SinOsc.ar(modulator));
}.scope;
)</pre>

<p>Side bands result in: <em>sum of C and M (1000+400=1400) and difference of C and M (1000-400=600)</em><pre class='code prettyprint lang-sc'>(
{var carrier, modulator;
    carrier = 100;
    modulator = 400;
    SinOsc.ar(carrier,0,SinOsc.ar(modulator));
}.scope;
)</pre>

<p>Side bands result in: <em>sum of C and M (100+400=500) and difference of C and M (100-400=-300) - negative frequencies change the sign of the phase of signal</em><pre class='code prettyprint lang-sc'>(
{var carrier, modulator;
    carrier = 100;
    modulator = 400;
    SinOsc.ar(carrier,0,SinOsc.ar(modulator))+SinOsc.ar(300,MouseX.kr(0,-0.5pi));
}.scope;
)</pre>

<p>Harmonic sidebands:<pre class='code prettyprint lang-sc'>{(SinOsc.ar(220,0,0.25)+SinOsc.ar(440,0,0.25))*SinOsc.ar(880)}.scope; //C and M are within harmonic ratios</pre>

<p>Inharmonic sidebands:<pre class='code prettyprint lang-sc'>{(SinOsc.ar(100,0,0.25)+SinOsc.ar(380,0,0.25))*SinOsc.ar(775)}.scope;</pre>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/ring2.png'/><br><b>Sidebands in Ring Modulation</b></div>
<h3><a class='anchor' name='Amplitude%20Modulation%20(AM):'>Amplitude Modulation (AM):</a></h3>
<ul>
<li>Unipolar<li>Like RM, AM generates a pair of sidebands for each sinusoidal component in the carrier and modulator<li>Sidebands are separated from the carrier by the distance corresponding to the inverse of the period of the modulator.<li>AM is the same result of RM + Carrier frequency</ul>
<pre class='code prettyprint lang-sc'>(
{var carrier, modulator;
    carrier = 500;
    modulator = 50;
    SinOsc.ar(carrier,0,SinOsc.ar(modulator, add:1, mul:0.5))*0.5;
}.scope;
)</pre>

<p>Side bands result in: <em>sum of C and M (1000+400=1400) and difference of C and M (1000-400=600) + C (1000)</em><pre class='code prettyprint lang-sc'>(
{var carrier, modulator;
    carrier = 1000;
    modulator = 400;
    SinOsc.ar(carrier,0,SinOsc.ar(modulator, add:1, mul:0.5))*0.5;
}.scope;
)</pre>

<p>In AM, high frequency causes sidebands but also maintains carrier frequency:<pre class='code prettyprint lang-sc'>b = Buffer.loadDialog;
{PlayBuf.ar(1, b.bufnum)*SinOsc.ar(440, add:1, mul:0.5)}.play</pre>

<p>In RM, high frequency causes sidebands:<pre class='code prettyprint lang-sc'>{PlayBuf.ar(1, b.bufnum)*SinOsc.ar(440)}.play</pre>
<h3><a class='anchor' name='Frequency%20Modulation%20(FM)'>Frequency Modulation (FM)</a></h3>
<ul>
<li>FM not a technique but a series of methods<li>PM (Phase modulation) and FM are virtually identical cases of angle modulation. Only difference is that the amplitudes generated by two methods are slightly different</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/fm1.png'/><br><b>Simple Frequency Modulation Patch</b></div>
<ul>
<li>Plug the modulator to the carrier frequency<li>Carrier Frequency C<li>Modulation Frequency M</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/fm2.png'/><br><b>FM sidebands</b></div>
<ul>
<li>FM generates sidebands around C Freq<li>Each sideband spreads out at a distance equal to a multiple of the modulating frequency<li>The position of sidebands depends on ratio of carrier and modulation frequency<li>When ratio is a simple integer (e.g. 4:1), then FM generates harmonic spectra<li>When ratio is not a simple integer (e.g. 8:2.1), then FM generates inharmonic spectra</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/fm3.png'/><br><b>FM sidebands</b></div>
<ul>
<li>Modulation index and bandwidth<li>Modulation index = frequency deviation / modulator frequency (I = D/M)<li>As I increases, so does sidebands and energy is “stolen” for carrier and distributed in sidebands<li>Frequencies above Nyquist frequency are reflected as well (folds over - aliases)<li>Lower sidebands (bellow 0Hz reflect back at 180 degree phase-inverted form)<li>Negative frequency components add richness to lower-frequency portion of spectrum<li>Amplitude of individual sideband components vary according to Bessel functions</ul>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/fm4.png'/><br><b>3d graph of Bessel functions as function of modulation index</b></div>

<p><strong>FM in SC</strong>:
<p>Can be done (without envelope) in the following way:<pre class='code prettyprint lang-sc'>(
var carrierFreq = 400, modFreq = 50, deviation = 100;
{    SinOsc.ar(carrierFreq + SinOsc.ar(modFreq, mul: deviation),
    mul: 0.3);
}.scope
)</pre>

<p>the same can be expressed:<pre class='code prettyprint lang-sc'>(
var carrierFreq = 400, modFreq = 50, deviation = 100;
{SinOsc.ar(SinOsc.ar(modFreq,mul: deviation, add: carrierFreq),
    mul: 0.3);
}.scope
)</pre>

<p><ul>
<li><a href="https://en.wikipedia.org/wiki/John_Chowning">John Chowning</a> (1967)<li><a href="https://en.wikipedia.org/wiki/Yamaha_DX7">Yamaha DX7</a><li>Cheap in CPU</ul>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/guides/guide2/fm5.png'/><br><b>Yamaha DX7</b></div>
<pre class='code prettyprint lang-sc'>(
{var carrfreq, modfreq, harmonicity, modindex;
//fc is frequency of carrier
//fm is frequency of modulator
carrfreq= MouseY.kr(330,550);
harmonicity= MouseX.kr(0,10).round(1);
modindex= MouseY.kr(0.0,10.0); //which is really modulation amplitude/modulation frequency, acts as brightness control as energy distribution changed over components
modfreq= carrfreq*harmonicity; //since harmonicity is an integer,
SinOsc.ar(carrfreq+(SinOsc.ar(modfreq)*modfreq*modindex), 0.0,0.1);
}.play
)

(
a = {arg carrfreq=400, harmonicity=4, modindex=1, atk=0.01, rel=1;
    var modfreq, env;
modfreq= carrfreq*harmonicity;
    env = Env.perc(atk, rel).kr(2);
SinOsc.ar(carrfreq+(SinOsc.ar(modfreq)*modfreq*modindex), 0.0,0.5)*env;
}
)

a.play(args: [\harmonicity, rrand(2,10).postln]); //evaluate several times
a.play(args: [\modindex, rrand(2,20.0).postln]); //evaluate several times

(
{inf.do{
    a.play(args: [\harmonicity, rrand(2,10.0), \modindex, rrand(2,20.0), \freq, (Scale.minor.degrees.choose+36).midicps, \atk, rrand(0.001,0.1), \rel, rrand(0.4,1) ]);
    [0.2,0.4,0.1,0.8].choose.yield;
};
}.fork;
)</pre>
<h3><a class='anchor' name='Phase%20modulation'>Phase modulation</a></h3>

<p>Similar to FM, but we modulate the phase instead of the frequency:
<p><code class='code prettyprint lang-sc'>PMOsc.ar(carfreq, modfreq, index, modphase, mul, add)</code><ul>
<li><em>carfreq</em> - carrier frequency in cycles per second.<li><em>modfreq</em> - modulator frequency in cycles per second.<li><em>index</em> is modulation index in radians<li><em>modphase</em> a modulation input for the modulator phase in radians</ul>

<p><pre class='code prettyprint lang-sc'>{ PMOsc.ar(Line.kr(600, 900, 5), 600, 3, 0, 0.1) }.play; // modulates carfreq

{ PMOsc.ar(300, Line.kr(600, 900, 5), 3, 0, 0.1) }.play; // modulates modfreq

{ PMOsc.ar(300, 550, Line.ar(0,20,8), 0, 0.1) }.play; // modulates index

(
var carrierFreq = 400, modFreq = 50, index = 3, decayTime = 5;
{ Env.perc(0.001,decayTime,0.2).kr(2) *
    PMOsc.ar(carrierFreq, modFreq, index,0);
}.scope
)

(
var carrierFreq = 400, modFreq,cmRatio = 1.5, index = 3, decayTime = 5;
modFreq = (carrierFreq * 1/cmRatio).postln;
{ Env.perc(0.001,decayTime,0.2).kr(2) *
    PMOsc.ar(carrierFreq, modFreq, index,0);
}.scope
)</pre>

<p>See also:<ul>
<li><code class='code prettyprint lang-sc'>SinOscFB</code> for feedback FM; a bit of the output is leaked back into the frequency input<li><code class='code prettyprint lang-sc'>Vibrato</code> to add vibrato (slow frequency modulation) potentially at some delay after onset</ul>

<p><div class='doclink'>helpfile source: <a href='file:///Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/SSfMT/HelpSource/SSfMT/Lectures/Digital Synthesis.schelp'>/Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/SSfMT/HelpSource/SSfMT/Lectures/Digital Synthesis.schelp</a><br>link::SSfMT/Lectures/Digital Synthesis::<br>sc version: 3.8.0</div></div></body></html>