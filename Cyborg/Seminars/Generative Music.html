<html><head><title>Generative Music</title>
<link rel='stylesheet' href='./../../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../../scdoc.js' type='text/javascript'></script>
<script src='./../../docmap.js' type='text/javascript'></script>
<script src='./../../prettify.js' type='text/javascript'></script>
<script src='./../../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./../..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider CYBORG/SEMINARS (extension)</div>
<div id='categories'><a href='./../../Browse.html#Tutorials/Cyborg'>Tutorials/Cyborg</a></div>
<h1>Generative Music</h1>
<div id='summary'>Cyborg Music</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../../Cyborg.html">Cyborg Music</a>, <a href="./../../Cyborg/Seminars/Computation and Music.html">Computation and Music</a>, <a href="./../../Cyborg/Seminars/Creativity and Music.html">Creativity and Music</a></div>
</div>
<div id='toc'>
<ul class='toc'></ul></div><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.001.png'/><br><b>Johann Wolfgang von Goethe - wikipedia.org</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.002.png'/><br><b>In Handbook of Research on Creativity, 2013</b></div>

<p><pre class='code prettyprint lang-sc'>(
{var n;
n=10;
Resonz.ar(
Mix.arFill(n,{
var freq, numcps;
freq= rrand(50,560.3);
numcps= rrand(2,20);
Pan2.ar(Gendy1.ar(6.rand,6.rand,1.0.rand,1.0.rand,freq ,freq, 1.0.rand, 1.0.rand, numcps, SinOsc.kr(exprand(0.02,0.2), 0, numcps/2, numcps/2), 0.5/(n.sqrt)), 1.0.rand2)
})
,MouseX.kr(100,2000), MouseY.kr(0.01,1.0))
;
}.play
)</pre>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.003.png'/><br><b>Brian Eno: 77 Million Paintings</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.004.png'/><br><b>Brian Eno: 77 Million Paintings</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.005.png'/><br><b>Brian Eno: 77 Million Paintings</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.006.png'/><br><b>Brian Eno: 77 Million Paintings</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.007.png'/><br><b>In Brian Eno, Visual Music, C. Scoates, 2013</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.008.png'/><br><b>Generative Systems and Gardening</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.009.png'/><br><b>In The Cambridge Companion to Electronic Music, 2007</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.010.png'/></div>

<p><a href="https://youtu.be/co9Q5BZDunI">Brian Eno, Atmospheric Lightness</a><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.011.png'/><br><b>Koan Pro and Generative Music 1 - intermorphic.com, iomoio.com</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.012.png'/></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.013.png'/><br><b>intermorphic.com/sseyo/koan/generativemusic1/</b></div>

<p><pre class='code prettyprint lang-sc'>(
{var a, c, d, s, z, y;
d = 6;
c = 5;
a = 4;
    s = Mix.ar(Array.fill(d, { Resonz.ar(Dust.ar(2/d, 50), 200 + 3000.0.rand, 0.003)}) );
    z = DelayN.ar(s, 0.048);
    y = Mix.ar(CombL.ar(z, 0.1, LFNoise1.kr(Array.fill(c,{0.1.rand}), 0.04, 0.05), 15));
    a.do({ y = AllpassN.ar(y, 0.050, [0.050.rand,0.050.rand], 1) });
    s+(0.2*y)
}.play
)</pre>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.014.png'/><br><b>Wind Chimes - ghar360.com</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.015.png'/><br><b>intermorphic.com/sseyo/koan/generativemusic1/</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.016.png'/><br><b>intermorphic.com/sseyo/koan/generativemusic1/</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.017.png'/><br><b>Gramophone - emiarchivetrust.org</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.018.png'/><br><b>intermorphic.com/sseyo/koan/generativemusic1/</b></div>

<p><pre class='code prettyprint lang-sc'>//Generative remix by Nick Collins (2007) - evaluate each section of code:
b = Buffer.loadDialog(s); //you need to change the path to a soundfile in your computer

(
~tempo = 95; //change to bpm of track
~signature = 4;
~barlength= ~tempo/120;
~spb= (~barlength/~signature);
~bps= (~spb).reciprocal;

~length= b.numFrames/44100.0;

~bars= (~length/~barlength).round.asInteger;

~framesperbar= ~barlength*44100.0;

SynthDef(\stepplaybuf2,{arg bufnum=0,outbus=0,rate=1,startPos=0,dur=0.1,atk=0.005, rel=0.05, curve=0;
var playbuf, env;
playbuf= PlayBuf.ar(2,bufnum,BufRateScale.kr(bufnum)*rate,1,startPos,1);
env= EnvGen.ar(Env.linen(atk,(dur-atk-rel),rel, 1.0, curve),doneAction:2);
Out.ar(outbus,playbuf*env);
}).send(s);

SynthDef(\step15bass,{arg outbus=0,dur=0.2, index=2, modfreq=15;
var playbuf;
playbuf= LFTri.ar(SinOsc.ar(modfreq,0,modfreq*Line.kr(index,0,dur),(Line.kr(44+12,32,dur)).midicps),0, EnvGen.ar(Env([0,1,1,0],[0.001,dur*0.5,dur*0.5]),doneAction:2));

Out.ar(outbus,Pan2.ar(playbuf,0));
}).send(s);

SynthDef(\step15fx1,{arg panrate1=15, panrate2=0.15, delay=0.01, decay=1.5876;
var input;
input= In.ar(0,2);
input= Pan2.ar(input[0],SinOsc.ar(panrate1,0))+ Pan2.ar(input[1], SinOsc.ar(panrate2,0));
Out.ar(0,EnvGen.ar(Env([0,0.5,0.5,0],[~barlength, 2*~barlength, ~barlength]),doneAction:2)*CombN.ar(input,~spb,delay, decay));
}).send(s);

SynthDef(\step15fx2,{arg amp=0.5;
var input;
input= In.ar(0,2);
Out.ar(0,EnvGen.ar(Env([0,1,1,0]*amp,[0.001,~barlength,0.01]),doneAction:2)*Limiter.ar(LPF.ar(Rand(5,10)*input,500)));
}).send(s);

SynthDef(\step15fx3,{arg amp=0.1, wsize=0.05, ratio=1, pdisp=0.0, tdisp=0;
var input;
input= In.ar(0,2);
ReplaceOut.ar(0,EnvGen.ar(Env([0,1,1,0]*amp,[0.001,~barlength,0.01]),doneAction:2)*PitchShift.ar(input, wsize, ratio, pdisp, tdisp));
}).send(s);

SynthDef(\step15limiter,{
var input;
input=In.ar(0,2);
ReplaceOut.ar(0,Limiter.ar(FreeVerb.ar(input,0.2), 0.99,0.01));
}).send(s);
)

(
var group1, fxgroup;
var patterns, pattnow;
var barsdone=0;
var count=[0,0,0];
var combfx;
group1= Group.new;
fxgroup= Group.after(group1);
Synth.tail(fxgroup, \step15limiter);
patterns=[
[[1,5.0]],
[[2,2.5]],
[[1,3.0],[1,2.0]],
[[1,3.0],[1,2.0]],
[[1,3.0],[4,0.5]],
[[1,4.0],[4,0.25]],
[[1,4.0],[3,0.3334]],
[1,1.0].dup(5),
[[1,1.5],[1,1.5],[2,1]],
[[1,1.5],[1,1.5],[2,1]],
[[1,1.5],[1,1.5],[2,1]],
[[2,1],[3,1.5],[2,1]],
[[2,1],[3,1.5],[2,1]],
{var roll; roll= rrand(1,3)*5; [[1,3],[roll,1.0/roll]]},
[3,0.33334].dup(5),
{var rolls; rolls= Array.rand(5,3,15); rolls.collect{|val| [val,1.0/val]}}
];
t= {
~bars.do { var pos, bassnow, longbass;
if((0.05.coin),{
s.sendBundle(s.latency, ["/s_new", \step15fx1, -1, 0, fxgroup.nodeID, \panrate1, 15, \panrate2, 0.15, \delay, [0.0096314826759936, 0.019262965351987,~spb*0.25, ~spb*0.5, ~spb].choose, \decay, [~spb, 1.5876].choose, \amp, [0.1,0.3,0.5,1.0].choose]);
});
if((0.1.coin),{
s.sendBundle(s.latency, ["/s_new", \step15fx2, -1, 0, fxgroup.nodeID, \amp, [0.1,0.3,0.5,1.0].choose]);
});
if((0.05.coin),{
s.sendBundle(s.latency, ["/s_new", \step15fx3, -1, 0, fxgroup.nodeID, \amp, [0.3,0.5,1.0].choose, \wsize, [0.01,0.05,0.1].choose, \ratio,[1,2,8,0.25,exprand(0.25,8)].choose, \pdisp, [0.0,rrand(0.0,2.0)].choose, \tdisp, [0.0,rrand(0.0,0.1)].choose]);
});
([1,2,3].wchoose([0.95,0.03,0.02])).do {|k|
{
pattnow= patterns.choose.value;
pattnow.postln;
bassnow=0.3.coin;
longbass=0.3.coin;
pos=0.0;
    if((0.15.coin) &amp;&amp; (barsdone&gt;10),{count[k]=rrand(1,3)});
    if(count[k]&gt;0,{pattnow=[[1,5.0]]; count[k]=count[k]-1;});
    pattnow.do{|cuts|
    var offset, dur, cutlength;
    var barpos;
    cuts.postln;
    barpos= rrand(max(0,barsdone-10),(barsdone+10).min(~bars-1));
    if(0.4.coin,{barpos= barsdone;});
    if(0.05.coin &amp;&amp; (barpos&gt;50),{barpos= rrand(0,~bars-1);});
    if(count[k]&gt;0,{if(0.9.coin,{barpos= barsdone;}); });
    offset= ((barpos)*~framesperbar) + (if(0.7.coin,0,{(pos*~spb*44100.0)}));
    dur= cuts[1];
    cutlength= if(0.9.coin,1,{if(0.8.coin,2,0.25)})*dur*~spb;
    cuts[0].do{
    s.sendBundle(s.latency, ["/s_new", \stepplaybuf2, -1, 0, group1.nodeID, \out, 0, \bufnum, b.bufnum, \rate, 1, \startPos,offset,\dur, cutlength]);
    if(bassnow, {
    s.sendBundle(s.latency, ["/s_new", \step15bass, -1, 0, group1.nodeID, \out, 0, \dur, if(longbass,cutlength,0.2)]);
    });
    pos=pos+dur;
    cutlength.wait;
    };
    }
}.fork;
};
barsdone= barsdone+1;
~barlength.wait;
}
}.fork
)

t.stop;
b.free;</pre>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar4/seminar 4.016.png'/><br><b>Andrey Markov - wikipedia.org</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar4/seminar 4.017.png'/></div>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar4/seminar 4.018.png'/><br><b>2-state Markov process</b></div>

<p><pre class='code prettyprint lang-sc'>(
SynthDef(\sinePerc, {arg freq=400, out=0, amp=0.5;
    var sig;
    sig = SinOsc.ar(freq,0,amp)*EnvGen.kr(Env.perc(0.01,0.1), doneAction: 2);
    Out.ar(out, sig!2);
}
).add;
)

//Simple 2-state Markov Chain
(
~mChain = [
[4, [4, 9], [0.3,0.7]], //element, nextElement, weights
[9, [4, 9], [0.4,0.6]]
]
)

(
~index = ~mChain.flop[0].indexOf(4);
{ inf.do{
~note = ~mChain[~index][1].choose(~mChain[~index][2]);
    Synth(\sinePerc, [\freq, (72+~note).midicps]);
~index = ~mChain.flop[0].indexOf(~note);
0.2.yield;
}
}.fork
)</pre>
<pre class='code prettyprint lang-sc'>//Zero order transition table (weighted random)
(
{ inf.do{
Synth(\sinePerc, [\freq, [72, 71, 69].wchoose([0.25, 0.5, 0.25]).midicps]);
    0.2.yield;
};
}.fork;
)

//another example using text as data
//the ascii of each character of the text is used for statistical analysis
//derived probabilities from text and converted into midi note values

(
SynthDef(\my_synth, {|dur = 1.0, amp = 1.0, freq = 440|
    var env = EnvGen.ar(Env.new([1, 0.1, 0], [0.06, dur - 0.06]), doneAction: 2);
    Out.ar([0, 1], SinOsc.ar([freq * 0.995, freq * 1.005], 0, env * amp))
}).add;
~text = "Serialism, along with John Cage's indeterminate music (music composed with the use of chance operations), and Werner Meyer-Eppler's aleatoricism, was enormously influential in post-war music. Theorists such as George Perle codified serial systems, and his 1962 text Serial Composition and Atonality became a standard work on the origins of serial composition in the work of Schoenberg, Berg and Webern.
Several of the composers associated with Darmstadt, notably Karlheinz Stockhausen, Karel Goeyvaerts, and Henri Pousseur developed a form of serialism that initially rejected the recurring rows characteristic of twelve-tone technique, in order to eradicate any lingering traces of thematicism (Felder 1977, 92). Instead of a recurring, referential row, each musical component is subjected to control by a series of numerical proportions (Morgan 1975, 3). In Europe, the style of some serial as well as non-serial music of the early 1950s emphasized the determination of all parameters for each note independently, often resulting in widely spaced, isolated points of sound, an effect called first in German punktuelle Musik (pointist or punctual music), then in French musique ponctuelle, but quickly confused with pointillistic (German pointillistische, French pointilliste) the familiar term associated with the densely packed dots in paintings of Seurat, despite the fact that the conception was at the opposite extreme (Stockhausen and Frisius 1998, 451).
Pieces were structured by closed sets of proportions, a method closely related to certain works from the de Stijl and Bauhaus movements in design and architecture called serial art by some writers (Bochner 1967, Gerstner 1964, Guderian 1985, Sykora 1983), specifically the paintings of Piet Mondrian, Theo van Doesburg, Bart van Leck, Georg van Tongerloo, Richard Paul Lohse, and Burgoyne Diller, who had been seeking to “avoid repetition and symmetry on all structural levels and working with a limited number of elements” (Bandur 2001, 54).
Stockhausen described the final synthesis in this manner:
So serial thinking is something that's come into our consciousness and will be there forever: it's relativity and nothing else. It just says: Use all the components of any given number of elements, don't leave out individual elements, use them all with equal importance and try to find an equidistant scale so that certain steps are no larger than others. It's a spiritual and democratic attitude toward the world. The stars are organized in a serial way. Whenever you look at a certain star sign you find a limited number of elements with different intervals. If we more thoroughly studied the distances and proportions of the stars we'd probably find certain relationships of multiples based on some logarithmic scale or whatever the scale may be. (Cott 1973, 101)
Igor Stravinsky's adoption of twelve-tone serial techniques offers an example of the level of influence that serialism had after the Second World War. Previously Stravinsky had used series of notes without rhythmic or harmonic implications (Shatzkin 1977). Because many of the basic techniques of serial composition have analogs in traditional counterpoint, uses of inversion, retrograde and retrograde inversion from before the war are not necessarily indicative of Stravinsky adopting Schoenbergian techniques. However with his meeting Robert Craft and acquaintance with younger composers, Stravinsky began to consciously study Schoenberg's music, as well as the music of Webern and later composers, and began to use the techniques in his own work, using, for example, serial techniques applied to fewer than 12 notes. Over the course of the 1950s he used procedures related to Messiaen, Webern and Berg. While it is difficult to label each and every work as serial in the strict definition, every major work of the period has clear uses and references to its ideas.
During this period, the concept of serialism influenced not only new compositions but also the scholarly analysis of the classical masters. Adding to their professional tools of sonata form and tonality, scholars began to analyze previous works in the light of serial techniques; for example they found the use of row technique in previous composers going back to Mozart and Beethoven (Jalowetz 1944, 387; Keller 1955, passim). In particular, the orchestral outburst that introduces the development section half-way through the last movement of Mozart's next-to-last symphony is a tone row that Mozart punctuates in a very modern and violent episode that Michael Steinberg called rude octaves and frozen silences (Steinberg 1998, 400).
Furthermore, the organizing principles of serialism inspired mathematical analogues, such as uses of set theory, group theory, operators, and parametrization, for example in the post-war works of Elliott Carter, Iannis Xenakis, and Witold Lutosławski.[citation needed] Likewise, the mathematical analogues in integral serialism were influential in the development of electronic music and synthesized music. Ruth Crawford Seeger is credited with extending serial controls to parameters other than pitch and to formal planning as early as 1930–33 (Tick 2001)";
~update = {//have to evaluate this to run new text
~smallLetters = (97..122).asAscii;
~capitalLetters = (65..90).asAscii;
~probabilities = [];
~smallLetters.do{|item, index|
        ~probabilities = ~probabilities.add(
            ~text.findAll(item.asString).size +
                ~text.findAll(~capitalLetters[index]).size / ~text.size;
    )
    };
~probabilities = ~probabilities++[1-~probabilities.sum];
};
)
//routine to play notes
(
~update.value;
{
var note;
inf.do{
note = (48..(48+~probabilities.size)).wchoose((~probabilities));
if(note == 74, {"silence".postln}, {
Synth(\my_synth, [\freq, note.postln.midicps, \dur, 0.2])
});
0.2.yield;
~update.value;
}
}.fork
)
//change text input
~text = "Hello";

~text = "Hi there";

~text = "a   abc zy";

~text = "aaa a aaz";

~text = "a";</pre>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar4/seminar 4.021.png'/></div>

<p><pre class='code prettyprint lang-sc'>(
~mChain = [
    [0, [7, 5, 4], [0.5,0.3, 0.2]], //element, nextElement, weights
    [7, [0], [1]],
    [5, [7, 4], [0.5,0.5]],
    [4, [0,5, 7], [0.2,0.4,0.4]]
    ];
)

(
~index = ~mChain.flop[0].indexOf(4);
{ inf.do{
~note = ~mChain[~index][1].choose(~mChain[~index][2]);
    Synth(\sinePerc, [\freq, (72+~note).midicps]);
~index = ~mChain.flop[0].indexOf(~note);
0.2.yield;
}
}.fork
)</pre>
<div class='image'><img src='http://www.squib-box.com/fr/cyborg/seminars/seminar8/seminar8.008.png'/><br><b>Robot reading - royalsociety.org</b></div>
<pre class='code prettyprint lang-sc'>//now a musical example:
//this is our melody which we will train the markov with
~notes = [ 0, 0, 7, 7, 9, 9, 7, 7, 5, 5, 4, 4, 2, 2, 0, 0 ] + 60;

Pbind(\midinote, Pseq(~notes, 1), \dur, 0.3).play

~pairs = ~notes.slide(2).slice;

~num = (~pairs.size/2);
~secondPair = Array.iota(~num.asInteger,2);
~trainMe = ~pairs.reshapeLike(~secondPair);

m = MarkovSet.new;

~trainMe.do{|item| m.read(item[0], item[1]) };

Pbind(\midinote, Pfsm2(m), \dur, 0.5).play;</pre>
<div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.038.png'/><br><b>Generative Music 1 at the Science Museum - wikipedia.org</b></div>

<p><div class='image'><img src='http://www.squib-box.com/fr/computerdotplay/seminars/seminar2/seminar2.039.png'/></div>

<p><a href="https://youtu.be/fD2IFSA7Koc">Brian Eno, I Dormienti</a><div class='doclink'>helpfile source: <a href='file:///Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/Cyborg/HelpSource/Cyborg/Seminars/Generative Music.schelp'>/Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/Cyborg/HelpSource/Cyborg/Seminars/Generative Music.schelp</a><br>link::Cyborg/Seminars/Generative Music::<br>sc version: 3.8.0</div></div></body></html>