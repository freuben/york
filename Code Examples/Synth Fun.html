<html><head><title>Synth Fun</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider CODE EXAMPLES (extension)</div>
<div id='categories'><a href='./../Browse.html#Tutorials/ComputerDotPlay'>Tutorials/ComputerDotPlay</a></div>
<h1>Synth Fun</h1>
<div id='summary'>Computer.play(Music, Creativity &amp; Code)</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../Computer.html">Computer.play</a>, <a href="./../Workshops/Patterns and Ndefs.html">Patterns and Ndefs</a>, <a href="./../Workshops/Interfacing.html">Interfacing</a>, <a href="./../Code Examples/FX Fun.html">FX Fun</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#Some%20Synths%20for%20you!'>Some Synths for you!</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Now%20have%20some%20fun!'>Now have some fun!</a></li>
<ul class='toc'></ul></ul></div><h2><a class='anchor' name='Some%20Synths%20for%20you!'>Some Synths for you!</a></h2>

<p>To add SynthDefs, run this code:<pre class='code prettyprint lang-sc'>(
~synthDefs = [
    SynthDef(\prophet5pwmstrings,{|out= 0 freq = 440 bend = 0 amp = 1.0 gate=1
        lforate = 10 lfowidth= 0.5 cutoff= 12000 rq=0.5 pan = 0.0|
        var lfo, pulse, filter, env;
        lfo = LFTri.kr(lforate*[1,1.01],Rand(0,2.0)!2);
        pulse = Pulse.ar((freq*bend.lag(0.1).midiratio)*[1,1.01],lfo*lfowidth+0.5);
        filter = RLPF.ar(pulse,cutoff,rq);
        env = EnvGen.ar(Env.adsr(0.01,0.0,1.0,0.5),gate,doneAction:2);
        Out.ar(out,Pan2.ar(Mix(filter)*env*amp*0.5,pan));
    }),
    SynthDef(\singleoscillatorwobble,{|out= 0 freq = 440 bend = 0 amp = 1.0 gate=1
        lforate = 10 lfowidth= 0.5 cutoff= 12000 rq=0.5 pan=0.0|
        var lfo, pulse, filter, env;
        lfo = LFTri.kr(lforate,Rand(0,2.0)!2);
        pulse = Pulse.ar((freq*bend.lag(0.1).midiratio)*(1.0+(lfowidth*lfo)),0.5);
        filter = RLPF.ar(pulse,cutoff,rq);
        env = EnvGen.ar(Env.adsr(0.01,0.0,1.0,0.5),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp*0.5,pan));
    }),
    SynthDef(\trianglewavebells,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1 lforate = 10
        lfowidth= 0.0 cutoff= 100 rq=0.5 pan=0.0|
        var osc1, osc2, vibrato, filter, env;
        vibrato = SinOsc.ar(lforate,Rand(0,2.0));
        osc1 = Saw.ar((freq*bend.lag(0.1).midiratio)*(1.0+(lfowidth*vibrato)),0.75);
        osc2 = Mix(LFTri.ar(((freq*bend.lag(0.1).midiratio).cpsmidi+[11.9,12.1]).midicps));
        filter = RHPF.ar((osc1+(osc2*0.5))*0.5,cutoff,rq);
        env = EnvGen.ar(Env.adsr(0.01,0.1,1.0,0.5),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\organdonor,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1 lforate = 10
        lfowidth= 0.0 cutoff= 100 rq=0.5 pan=0.0|
        var vibrato, pulse, filter, env;
        vibrato = SinOsc.ar(lforate,Rand(0,2.0));
        pulse = Mix(VarSaw.ar(([1,1.9953843530485,3.0139733629359]
            *(freq*bend.lag(0.1).midiratio))*(1.0+(lfowidth*vibrato)),Rand(0.0,1.0)!3,
        Rand(0.3,0.5)!3,[1.0,0.7,0.3]))*0.5;
        filter = RLPF.ar(pulse,cutoff,rq);
        env = EnvGen.ar(Env.adsr(0.01,0.5,1.0,0.5),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\werkit,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1 cutoff= 100
        rq=0.1 pan=0.0|
        var source, filter, env;
        source = WhiteNoise.ar;
        filter = BLowPass4.ar(source,(freq*bend.lag(0.1).midiratio),rq)*0.3;
        env = EnvGen.ar(Env([0,1,0.5,0.0],[0.02,0.1,0.1]),doneAction:2);
        Out.ar(out,Pan2.ar((0.7*filter+(0.3*filter.distort))*env*amp,pan));
    }),
    SynthDef(\laserbeam,{|out= 0 freq = 440 bend = 0 amp = 0.1 atkTime= 0.04
        gate=1 pan=0.0|
        var osc1, freqenv, ampenv;
        freqenv = EnvGen.ar(Env([4,0.5,1,1],[atkTime,0.01,1.0]));
        osc1 = LFTri.ar((freq*bend.lag(0.1).midiratio)*freqenv);
        //env = EnvGen.ar(Env.adsr(0.01,0.0,1.0,0.1),gate,doneAction:2);
        //no gate, fixed envelope size
        ampenv = EnvGen.ar(Env([0,1,0.5,0.0],[0.02,0.2,0.1]),doneAction:2);
        Out.ar(out,Pan2.ar(osc1*ampenv*amp,pan));
    }),
    SynthDef(\moogbasstone,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1 cutoff= 1000
        gain=2.0 lag = 0.01 pan=0.0, lagFilter=0.01|
        var osc, filter, env, filterenv;
        osc = Mix(VarSaw.ar((freq*bend.lag(0.1).midiratio).lag(lag)*[1.0,1.001,2.0],
            Rand(0.0,1.0)!3,Rand(0.5,0.75)!3,0.33));
        filterenv = EnvGen.ar(Env.adsr(0.2,0.0,1.0,0.2),gate,doneAction:2);
        filter =  MoogFF.ar(osc,cutoff.lag(lagFilter)*(1.0+(0.5*filterenv)),gain);
        env = EnvGen.ar(Env.adsr(0.001,0.3,0.9,0.2),gate,doneAction:2);
        Out.ar(out,Pan2.ar((0.7*filter+(0.3*filter.distort))*env*amp*1.5,pan));
    }),
    SynthDef(\moogbasstone2,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        atkTime= 0.2 fenv=0.5 cutoff= 1000 gain=2.0 pan=0.0|
        var osc, filter, env, filterenv;
        osc = Mix(Pulse.ar((freq*bend.lag(0.1).midiratio).lag(0.05)*[1.0,1.001,2.0],
            Rand(0.45,0.5)!3,0.33));
        filterenv = EnvGen.ar(Env.adsr(atkTime,0.0,1.0,0.2),gate,doneAction:2);
        filter =  MoogFF.ar(osc,cutoff*(1.0+(fenv*filterenv)),gain);
        env = EnvGen.ar(Env.adsr(0.001,0.3,0.9,0.2),gate,doneAction:2);
        Out.ar(out,Pan2.ar((0.7*filter+(0.3*filter.distort))*env*amp,pan));
    }),
    SynthDef(\mrostinato,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1 lforate = 10
        lfowidth= 0.5 pan = 0.0|
        var lfo, pulse, filter, env;
        lfo = LFTri.kr(lforate,Rand(0,2.0)!3);
        pulse = Pulse.ar((freq*bend.lag(0.1).midiratio)*[1,1.01,0.5],lfo*lfowidth+0.5);
        env = EnvGen.ar(Env.adsr(0.01,0.05,0.5,0.1),gate,doneAction:2);
        Out.ar(out,Pan2.ar(Mix(pulse)*env*amp,pan));
    }),
    SynthDef(\plastickystrings,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        lforate = 5900 lfowidth= 0.01 cutoff= 12000 rq=0.5 pan=0.0|
        var lfo, saw, filter, env;
        lfo = LFTri.ar(lforate,Rand(0,2.0));
        saw = Saw.ar((freq*bend.lag(0.1).midiratio)*(1.0+(lfowidth*lfo)),0.5);
        filter = BHiPass.ar(saw,(freq*bend.lag(0.1).midiratio),rq);     //or using cutoff
        env = EnvGen.ar(Env.adsr(0.4,0.0,1.0,1.0),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\handclapsmash,{|out= 0 amp = 0.1 gate=1 cutoff= 12000 rq=0.5
        reltime=0.5 pan=0.0|
        var noise, filter, filterenv, env;
        noise = WhiteNoise.ar;
        filterenv = EnvGen.ar(Env([0.0,1.0,0.3,0.0],[0.0,0.3,0.3]));
        filter = BHiPass.ar(RLPF.ar(noise,cutoff*filterenv,rq),150);
        filter = BPeakEQ.ar(filter,440,1.0,8);
        filter = BPeakEQ.ar(filter,1200,1.0,14);
        env = EnvGen.ar(Env.perc(0.005,reltime),doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\handclapsmash2,{|out= 0 amp = 0.1 gate=1 cutoff= 12000 cutoff2=10000
        rq=0.5 mod=0.0 modrate=50 reltime=0.7 pan=0.0|
        var noise, filter, filterenv, modulation, env;
        noise = WhiteNoise.ar;
        filterenv = EnvGen.ar(Env([0.0,1.0,0.3,0.0],[0.0,0.3,0.3]));
        modulation = (LFSaw.ar(modrate).range(0.0,1.0))
        *EnvGen.ar(Env([1,1,0,0],[0.1,0.0,1.0]));
        filter = BHiPass.ar(RLPF.ar(noise,cutoff*filterenv+ (cutoff2*mod*modulation),rq),150);
        filter = BPeakEQ.ar(filter,440,1.0,8);
        filter = BPeakEQ.ar(filter,1200,1.0,14);
        env = EnvGen.ar(Env.perc(0.005,reltime),doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\bassfoundation,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff= 1000 rq=0.5 pan=0.0|
        var osc, filter, env, filterenv;
        osc = Saw.ar((freq*bend.lag(0.1).midiratio));
        filterenv = EnvGen.ar(Env.adsr(0.0,0.5,0.2,0.2),gate,doneAction:2);
        filter =  RLPF.ar(osc,cutoff*filterenv+100,rq);
        env = EnvGen.ar(Env.adsr(0.01,0.0,0.9,0.05),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp*2,pan));
    }),
    SynthDef(\basshighend,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff= 3000 rq=0.1 drive = 2.0 pan=0.0|
        var osc, filter, env, filterenv;
        var ab;
        osc = Mix(VarSaw.ar((freq*bend.lag(0.1).midiratio)*[0.25,1,1.5],
            Rand(0.0,1.0)!3,0.9,[0.5,0.4,0.1]));
        filterenv = EnvGen.ar(Env.adsr(0.0,0.5,0.2,0.2),gate,doneAction:2);
        filter =  RLPF.ar(osc,cutoff*filterenv+100,rq);
        filter = filter.distort.softclip;
        ab = abs(filter);
        filter = (filter*(ab + drive)/(filter ** 2 + (drive - 1) * ab + 1));
        filter = BLowShelf.ar(filter,300,1.0,-12);
        filter = BPeakEQ.ar(filter,1600,1.0,-6);
        env = EnvGen.ar(Env.adsr(0.01,0.0,0.9,0.05),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp*2,pan));
    }),
    SynthDef(\winwoodlead,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff=8000 rq=0.8 lfowidth=0.01 lforate= 8 lag=0.01 pan=0.0|
        var pulse, filter, env, lfo;
        lfo = LFTri.kr(lforate,Rand(0,2.0)!2);
        pulse = Mix(Pulse.ar(((freq*bend.lag(0.1).midiratio).lag(lag))*[1,1.001]
            *(1.0+(lfowidth*lfo)),[0.2,0.19]))*0.5;
        filter =  RLPF.ar(pulse,cutoff,rq);
        filter = BLowShelf.ar(filter,351,1.0,-9);
        env = EnvGen.ar(Env.adsr(0.01,0.0,0.9,0.05),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\spookystrings,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff=8000 rq=0.2 lfowidth=0.01 lforate=3.8 pan=0.0|
        var pulse, filter, env, lfo;
        lfo = LFTri.kr(lforate,Rand(0,2.0)!2);
        pulse = Mix(Saw.ar((freq*bend.lag(0.1).midiratio)*[1,1.001]
            *[1.0,1.0+(lfowidth*lfo)]))*0.5;
        filter = BHiPass.ar(pulse,cutoff,rq);
        env = EnvGen.ar(Env.adsr(0.1,0.0,1.0,3),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\spookystrings2,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff=8000 rq=0.2 pan=0.0|
        var saw, pulse, filter, env, lfo, pulsewidthlfo;
        lfo = LFTri.kr([3.5,5],Rand(0,2.0)!2);
        pulsewidthlfo = SinOsc.kr(4.5);
        saw = Saw.ar((freq*bend.lag(0.1).midiratio)*(1.0+(0.052*lfo[0])));
        pulse = Pulse.ar((freq*bend.lag(0.1).midiratio)*(1.0+(0.072*lfo[1])),
            pulsewidthlfo*0.25+0.5);
        pulse = 0.5*(saw+pulse);
        filter = BLowPass.ar(pulse,XLine.kr(678,cutoff,0.378),rq);
        env = EnvGen.ar(Env.adsr(0.1,0.0,1.0,3),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\synchronicity,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff=8000 rq=0.2 pan=0.0|
        var pulse, filter, env, slavefreq;
        slavefreq = EnvGen.ar(Env([1,5,1],[0.04,0.9]))*(freq*bend.lag(0.1).midiratio);
        pulse = SyncSaw.ar((freq*bend.lag(0.1).midiratio),slavefreq);
        filter = BLowPass.ar(pulse,cutoff,rq);
        env = EnvGen.ar(Env.adsr(0.01,0.0,1.0,0.1),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\situationsynth,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff=8000 rq=0.8 lfowidth=0.001 lforate= 3.3 pan=0.0|
        var pulse, filter, env, filterenv, lfo;
        lfo = LFTri.kr(lforate,Rand(0,2.0)!2);
        pulse = Mix(Pulse.ar(((((freq*bend.lag(0.1).midiratio).cpsmidi)+[0,0.14])
            +(lfo*lfowidth)).midicps,[0.5,0.51]+(lfowidth*lfo)))*0.5;
        filterenv = EnvGen.ar(Env([0.0,1.0,0.3,0.0],[0.005,0.57,0.1],-3));
        filter =  RLPF.ar(pulse,100+(filterenv*cutoff),rq);
        env = EnvGen.ar(Env.adsr(0.002,0.57,1.0,0.3),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\ressquares,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff=8000 rq=0.8 pan=0.0|
        var pulse, filter, env;
        pulse = Mix(Pulse.ar( (((freq*bend.lag(0.1).midiratio).cpsmidi)
            +[0,0.02]).midicps, 0.5))*0.5;
        filter =  BLowPass.ar(pulse,100+cutoff,rq);
        env = EnvGen.ar(Env.adsr(0.002,0.1,1.0,0.2),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\whoorgan,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff=8000 rq=0.8 pan=0.0|
        var osc, filter, env;
        var basefreq =  (((freq*bend.lag(0.1).midiratio).cpsmidi)+[0,0.1]).midicps;
        osc = Mix.fill(10,{|i| SinOsc.ar(basefreq*(i+1),0.0,(0.25+(0.75
            *LFNoise0.kr(10.1+i,0.5,0.5)))/(1.0+(0.5*i)))})*0.2;
        filter =  BLowPass.ar(osc,2000+(amp*cutoff),rq);
        env = EnvGen.ar(Env.adsr(0.02,0.1,0.9,1.0),gate,doneAction:2);
        Out.ar(out,Pan2.ar(Mix(filter)*env*amp,pan));
    }),
    SynthDef(\whoorgan2,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff=8000 rq=0.8 pan=0.0|
        var osc, filter, env;
        var basefreq =  (((freq*bend.lag(0.1).midiratio).cpsmidi)+[0,0.1]).midicps;
        osc = Mix(Blip.ar(basefreq,16))*0.5;
        filter =  BLowPass.ar(osc,2000+(amp*cutoff),rq);
        env = EnvGen.ar(Env.adsr(0.02,0.1,0.9,1.0),gate,doneAction:2);
        Out.ar(out,Pan2.ar(Mix(filter)*env*amp,pan));
    }),
    SynthDef(\eightoeightkick,{|out= 0 freq = 440 bend = 0 amp = 0.1 ringtime=10.0
        reltime=1.0 dist = 0.1 pan=0.0|
        var impulse, filter, env;
        impulse = Impulse.ar(0);
        filter = Ringz.ar(impulse,XLine.ar((freq*bend.lag(0.1).midiratio),60,0.1),ringtime);
        env = EnvGen.ar(Env.perc(0.001,reltime),doneAction:2);
        filter = (1.0-dist)*filter + (dist*(filter.distort));
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }),
    SynthDef(\pwmbling,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1 lforate = 4.85
        lfowidth= 0.5 cutoff= 12000 rq=0.25 pan = 0.0|
        var lfo, pulse, filter, env;
        var basefreq =  (((freq*bend.lag(0.1).midiratio).cpsmidi)+[0,12.12]).midicps;
        lfo = LFTri.kr(lforate*[1,1.01],Rand(0,2.0)!2);
        pulse = Pulse.ar(basefreq,lfo*lfowidth+0.5);
        env = EnvGen.ar(Env.adsr(0.0,1.0,0.2,1.5),gate,doneAction:2);
        filter = RLPF.ar(pulse,(cutoff*(env.squared))+100,rq);
        Out.ar(out,Pan2.ar(Mix(filter)*env*amp,pan));
    }),
    SynthDef(\tonewheeltwo,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        lforate = 4.85 lfowidth= 0.1 cutoff= 5000 rq=0.25 pan = 0.0|
        var lfo, pulse, filter, env;
        lfo = LFTri.kr(lforate*[1,1.01],Rand(0,2.0)!2);
        pulse = Pulse.ar( ((((freq*bend.lag(0.1).midiratio)*[1,3]).cpsmidi)
            + (lfo*lfowidth)).midicps,[0.5,0.51],[0.4,0.6]);
        env = EnvGen.ar(Env.adsr(0.0,0.0,1.0,0.1),gate,doneAction:2);
        filter = BLowPass4.ar(pulse,cutoff,rq);
        filter= BPeakEQ.ar(filter,500,1.0,3);
        Out.ar(out,Pan2.ar(Mix(filter)*env*amp,pan));
    }),
    SynthDef(\everythingrhodes,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        lforate = 4.85 lfowidth= 0.5 cutoff= 2000 rq=0.5 pan = 0.0|
        var pulse, filter, env;
        pulse = Mix.ar(Pulse.ar((freq*bend.lag(0.1).midiratio)
            *[1,33.5.midiratio],[0.2,0.1],[0.7,0.3]));
        env = EnvGen.ar(Env.adsr(0.0,1.0,0.8,3.0),gate,doneAction:2);
        filter = BLowPass4.ar(pulse,(cutoff)+200+(freq),rq);
        Out.ar(out,Pan2.ar(Mix.ar(filter)*env*amp,pan));
    }),
    SynthDef(\justwannahavefun,{|out= 0 freq = 440 bend = 0 amp = 0.1
        ringtime=10.0 pan=0.0|
        var impulse, filter, env;
        impulse = Impulse.ar(0);
        filter = Ringz.ar(impulse,((freq*bend.lag(0.1).midiratio).cpsmidi +
            (Rand(0.2,1.2)*SinOsc.kr(Rand(10,50)))).midicps,ringtime);
        env = EnvGen.ar(Env([0.0,1.0,1.0,0.0],[0.001,0.04,0.04]),doneAction:2);
        Out.ar(out,Pan2.ar((filter.softclip(0.9))*env*amp,pan));
    }),
    SynthDef(\spacetheremin,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        lforate = 6 lfowidth= 0.5 cutoff= 4000 rq=0.25 lag=0.1 pan=0.0|
        var lfo, osc, filter, env;
        lfo = LFTri.kr(lforate+(LFNoise1.kr(5,0.3,0.3)),Rand(0,2.0));
        osc = Saw.ar(((freq*bend.lag(0.1).midiratio).lag(lag).cpsmidi
            +(lfo*lfowidth)).midicps,0.5);
        filter = BLowPass4.ar(osc,cutoff.lag(lag*4),rq);
        env = EnvGen.ar(Env.adsr(0.6,0.0,1.0,0.05),gate,doneAction:2);
        Out.ar(out,Pan2.ar(filter*env*amp.lag(lag*4),pan));
    }),
    SynthDef(\fatvelocitybass,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff= 12000 rq=0.25 lag=0.01 pan = 0.0|
        var lfo, osc, filter, env;
        var basefreq =  (((freq*bend.lag(0.1).midiratio).lag(lag).cpsmidi)
            +[0,11.95,31.03]).midicps;
        osc = Saw.ar(basefreq,[0.5,0.4,0.1]);
        env = EnvGen.ar(Env.adsr(0.01,1.0,1.0,0.25),gate,doneAction:2);
        filter = BLowPass4.ar(osc,100+((amp.squared)*((freq*bend.lag(0.1).midiratio)
            +cutoff)),rq);
        Out.ar(out,Pan2.ar(Mix(filter)*env*amp*0.8,pan));
    }),
    SynthDef(\fatvelocitybass2,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff= 12000 rq=0.25 lag=0.01 pan = 0.0|
        var lfo, osc, filter, env;
        var basefreq =  (((freq*bend.lag(0.1).midiratio).lag(lag).cpsmidi)
            +[0,11.95,31.03]).midicps;
        osc = Saw.ar(basefreq,[0.5,0.4,0.1])+PinkNoise.ar(Line.kr(1.0,0,0.03));
        env = EnvGen.ar(Env.adsr(0.01,1.0,1.0,0.25),gate,doneAction:2);
        filter = BLowPass4.ar(osc,100+((amp.squared)*((freq*bend.lag(0.1).midiratio)
            +cutoff)),rq);
        Out.ar(out,Pan2.ar(Mix(filter)*env*amp*0.8,pan));
    }),
    SynthDef(\wavecross1, {|out=0 freq=440 bend=0 attack=0.01 dec=0.4 sus=0.3
        sus2=0.4, rel=2 amp=0.5 pan|
        var wave1, wave2, wave3, wave4, nowFreq;
        var env1,env2,env3,env4, globalEnv;
        env1 = EnvGen.kr(Env([0,1,0],[attack,dec]));
        env2 = EnvGen.kr(Env([0,0,1,0],[attack,dec,sus]));
        env3 = EnvGen.kr(Env([0,0,1,0],[attack+dec,sus,sus2]));
        env4 = EnvGen.kr(Env([0,0,1,0],[attack+dec+sus,sus2,rel]));
        globalEnv = EnvGen.kr(Env([0,1,0.5,0.5,0.4,0],[attack,dec,sus,sus2,rel]));
        nowFreq = (freq*bend.lag(0.1).midiratio);
        wave1 = Pulse.ar(nowFreq)*env1;
        wave2 = Formant.ar(nowFreq)*env2;
        wave3 = Saw.ar(nowFreq)*env3;
        wave4 = SinOsc.ar(nowFreq)*env4;
        Out.ar(out, Pan2.ar((wave1 + wave2 + wave3 + wave4), pan)*globalEnv*amp);
    }),
    SynthDef(\wavestack1, {|out=0 freq=440 bend=0 attack=1 sus=1 rel=1
        amp=0.5 pan|
        var globalEnv, harm, signal, envArr;
        var wave1, wave2, wave3, wave4, nowFreq;
        globalEnv = EnvGen.kr(Env.linen(0.01*attack,0.3*sus,2*rel), doneAction:2);
        [0.01*attack,0.3*sus,2*rel].sum*[[0.3*attack,0.6*sus,0.1*rel],[0.1*attack,0.5
            *sus,0.4*rel],[0.01*attack,0.3*sus,0.69*rel],[0.8*attack,0.1*sus,0.1
            *rel]].do{|item| envArr = envArr.add(Env.linen(item[0], item[1], item[2]))};
        harm = [1,2,3,4];
        nowFreq = (freq*bend.lag(0.1).midiratio);
        wave1 = Pulse.ar(freq*harm[3])*EnvGen.kr(envArr[0]);
        wave2 = Formant.ar(freq*harm[1])*EnvGen.kr(envArr[1]);
        wave3 = Saw.ar(freq*harm[2])*EnvGen.kr(envArr[2]);
        wave4 = SinOsc.ar(freq*harm[0])*EnvGen.kr(envArr[3]);
        signal = ((wave1 + wave2 + wave3 + wave4)/4)*globalEnv;
        Out.ar(out, Pan2.ar(signal*amp*2, pan));
    }),
    SynthDef(\simplesine,{|out= 0 freq = 440 bend = 0 amp = 1.0 gate=1 atk=0.01
        dec=0.0 sus=1.0 rel=0.5 pan=0.0|
        var signal, env;
        signal = SinOsc.ar(freq,0,0.5);
        env = EnvGen.ar(Env.adsr(atk,dec,sus,rel),gate,doneAction:2);
        Out.ar(out, Pan2.ar(signal*env*amp, pan));
    }),
    //add more if you want!
    SynthDef(\pitchshift,{|out= 0 freq = 440 bend = 0 amp = 1.0 gate=1 bus=0
        winSize=0.2 bassfreq=440 pitchDis=0.0, timeDis=0.01|
        var signal, env, in, pan;
        in = SoundIn.ar(bus);
        signal = PitchShift.ar(in, winSize, (freq/bassfreq), pitchDis, timeDis);
        env = EnvGen.ar(Env.linen(0.1,1.0,0.4),gate,doneAction:2);
        pan = freq.cpsmidi.linlin(21,108,-1,1);
        Out.ar(out, Pan2.ar(signal*env*amp, pan));
    });
];
s.waitForBoot{
    ~synthDefs.do{|item| item.add };
};
)</pre>

<p><div class='note'><span class='notelabel'>NOTE:</span> Most of these synths are coming from Mitchell Sigman (2011) Steal this Sound. Milwaukee, WI: Hal Leonard Books, Adapted for SuperCollider and elaborated by Nick Collins.</div><h2><a class='anchor' name='Now%20have%20some%20fun!'>Now have some fun!</a></h2>

<p>Again, this is a very basic example - you can do much more exciting things with these synths if you spend some time with them and combine them in interesting ways. You can also combine them with the effects in <a href="./../Code Examples/FX Fun.html">FX Fun</a>.<div class='note'><span class='notelabel'>NOTE:</span> Feel free to use this code for your project</div><pre class='code prettyprint lang-sc'>~synthDefs.do{|item| item.name.postln } //list synthdefs

Ndef(\play1).play;
Ndef(\play1).fadeTime = 1;

Ndef(\play1, \simplesine);
Ndef(\play1).set(\gate, 0);

Ndef(\play1).spawn(extraArgs: [\freq, rrand(300,500), \gate, 1]) //on

Ndef(\play1).set(\gate, 0); //off

//now some patterns
(
Pdef(\pat1, Pbind(\degree, 0));
Ndef(\play1, Pdef(\pat1));
)

Pdef(\pat1).quant = 8; //quantise (every 8 beats)

(
~pat = Pbind(\instrument, \wavestack1, \degree, Pseq((0..7).scramble, inf),
    \dur, 0.2);
Ndef(\play1, Pdef(\pat1,  ~pat) );
)

(
~arrp = Pbind(\instrument, \pwmbling, \degree, Pseq((0..7).scramble, inf),
    \dur, 0.2, \amp, 0.1);
Pdef(\pat1,  ~arrp);
)

(
~kick = Pbind(\instrument, \eightoeightkick, \degree, Pseq([0,\rest,0,\rest], inf),
    \dur, 0.2, \amp, 0.4);
Pdef(\pat1,  Ppar([~arrp, ~kick]) );
)

(
~snare = Pbind(\instrument, \handclapsmash, \degree, Pseq([\rest,\rest,\rest, 0], inf),
    \reltime, 0.5, \dur, 0.2);
Pdef(\pat1,  Ppar([~arrp, ~kick, ~snare]) );
)

(
~randBass = ((0..5) ++ Array.fill(5, \rest)).scramble;
~bass = Pbind(\instrument, \fatvelocitybass, \degree,  Pseq(~randBass, inf),
    \dur, 0.2, \amp, 0.3, \ctranspose, -24);
Pdef(\pat1,  Ppar([~arrp, ~kick, ~snare, ~bass]) );
)

(
Pdef(\pat1, Pbind(\instrument, \pwmbling, \degree, Pseq((0..7).scramble, inf),
    \dur, 0.2, \amp, 0.1) );
)

Pdef(\pat1,  Ppar([~arrp, ~kick, ~snare, ~bass]) );

Pdef(\pat1, 0);</pre>

<p><div class='doclink'>helpfile source: <a href='file:///Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/ComputerPlay/HelpSource/Code Examples/Synth Fun.schelp'>/Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/ComputerPlay/HelpSource/Code Examples/Synth Fun.schelp</a><br>link::Code Examples/Synth Fun::<br>sc version: 3.8.0</div></div></body></html>